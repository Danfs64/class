// Consider the following definition for natural numbers:
//
// Z:     The number zero
// (S x): The successor of a number x
//
// Ex.: The number 3 would be (S (S (S Z)))
//
// With that definition in mind, do the following exercises:

// PS.: Remember again that, if your function has specific cases,
// you need to write a version of the function for each case:
//
// (Func Z)     -> Will run only for Zero.
// (Func (S n)) -> Will run for every number that is not Zero.
//                 By calling the function in that way, you can use 'n' as a variable.
// (Func x)     -> Will run for any number.

// Returns the predecessor of the number
(Pred  Z   ) = Z
(Pred (S x)) = x

// Returns the Double of the number
(Double  Z   ) = Z
(Double (S x)) = (S (S (Double x)))

// Returns the Half of the number and round it down (e.g.:"5/2 = 2") 
(Half  Z       ) = Z
(Half (S  Z   )) = Z
(Half (S (S x))) = (S (Half x))

// Returns the Sum of two numbers
(Add  Z    y) = y
(Add (S x) y) = (S (Add x y))

// Returns the Subtraction of two numbers (0 if it would be negative)
(Sub  x     Z   ) = x
(Sub  Z     y   ) = Z
(Sub (S x) (S y)) = (Sub x y)

// Returns the Multiplication two numbers
(Mul x  Z   ) = Z
(Mul x (S y)) = (Add x (Mul x y))

// Returns the Factorial of the number
(Fact  Z   ) = (S Z)
(Fact (S x)) = (Mul (S x) (Fact x))

// For the following 3 exercises, consider the definition of Booleans from the previous file

// Returns if two numbers are equal
(Equal  Z     Z   ) = True
(Equal (S x) (S y)) = (Equal x y)
(Equal  x     y   ) = False

// Returns if the first arg is greater than the second
(Gtn  Z     y   ) = False
(Gtn  x     Z   ) = True
(Gtn (S x) (S y)) = (Gtn x y)

// Returns if the first arg is lesser than the second
(Ltn  x     Z   ) = False
(Ltn  Z     y   ) = True
(Ltn (S x) (S y)) = (Ltn x y)

// SPECIAL BOSS: Returns the division between two numbers, rounded down
// PS.: Returns 0 in case of division by 0
(Div x Z) = Z
(Div x y) = (Div.aux (Ltn x y) x y)
    (Div.aux True  x y) = Z
    (Div.aux False x y) = (S (Div (Sub x y) y))
